#!/usr/bin/env perl

use strict;
use warnings FATAL => 'all';
use English qw( PROGRAM_NAME );
use File::Basename;
use Log::Log4perl qw(:easy);
Log::Log4perl->easy_init($INFO);

# Define version
use constant MMM_VERSION => '!!!VERSION!!!';

# Include parts of the system
use MMM::Common::Config;
use MMM::Common::Socket;

# Maybe we were just asked for our version
if (scalar(@ARGV) && $ARGV[0] eq "--version") {
    printf "%s %s\n", basename($PROGRAM_NAME), MMM_VERSION;
    exit(0);
}

my $postfix = '';
if (scalar(@ARGV) && $ARGV[0] =~ /^@(.*)/) {
    shift(@ARGV);
    $postfix = "_$1";
}

# Read configuration
our $config = new MMM::Common::Config::;
$config->read("mmm_mon$postfix");
$config->check('CONTROL');

die "See '$0 help' for usage information" if (scalar(@ARGV) < 1);

my $socket = MMM::Common::Socket::create_sender($config->{monitor}->{ip}, $config->{monitor}->{port}, 10);
unless ($socket && $socket->connected) {
	print "ERROR: Can't connect to monitor daemon!\n";
	exit(2);
}

print $socket join(' ', @ARGV), "\nquit\n";
my $res = '';
my $line;
$res .= $line while ($line = <$socket>);
print $res, "\n";

# strip comments
$res =~ s/^#.*$//gm;
$res =~ s/\n+/\n/g;

if ($res =~ /ERROR/) {
	exit(2);
} elsif ($res =~ /UNKNOWN/) {
	exit(3);
}

# specific exit codes for 'show' command
if ($ARGV[0] eq 'show') {
	if (defined $ARGV[1] && $ARGV[1] eq 'status') {
		if ($res =~ /(HARD_OFFLINE|AWAITING_RECOVERY|REPLICATION_FAIL)/) {
			exit(2);
		} elsif ($res =~ /(ADMIN_OFFLINE|REPLICATION_DELAY)/) {
			exit(1);
		} elsif ($res =~ /ONLINE/) {
			exit(0);
		} else {
			exit(3);
		}
	} else {
		chomp $res;
		my @res = split /\n/, $res;
		
		my $nodes_count = scalar @res;
		my $online_count = scalar grep {/ONLINE/} @res;
		
		if ($nodes_count == $online_count) {
			exit(0);
		} elsif (($nodes_count - $online_count) == 1) {
			exit(1);
		} else {
			exit(2);
		}
	}
}

exit(0);

